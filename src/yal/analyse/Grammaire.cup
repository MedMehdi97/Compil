package yal.analyse ;

import java.util.*;
import yal.arbre.* ;
import yal.arbre.expressions.* ;
import yal.arbre.instructions.* ;
import yal.exceptions.AnalyseSyntaxiqueException;
import yal.tds.*;
import yal.tds.entree.*;
import yal.tds.symbole.*;
import java_cup.runtime.*;

action code {:
             :} ;

parser code {:

    public void report_error(String message, Object info) {

        HashMap<Integer, String> lesTerminaux = new HashMap<>() ;

        lesTerminaux.put(new Integer(CodesLexicaux.DEBUT), "debut") ;
        lesTerminaux.put(new Integer(CodesLexicaux.FIN), "fin") ;
        lesTerminaux.put(new Integer(CodesLexicaux.POINTVIRGULE), ";") ;

        StringBuffer m = new StringBuffer() ;

        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            if (s.left >= 0) {
                m.append("\tligne : " + (s.left + 1)) ;
                if (s.right >= 0)
                    m.append(" colonne : " + (s.right+1)) ;
            }

            if (s.value != null) {
                lesTerminaux.put(CodesLexicaux.CSTENTIERE, "" + s.value) ;
            }

            if (lesTerminaux.containsKey(new Integer(s.sym))) {
                m.append(" dernier token lu : " + lesTerminaux.get(new Integer(s.sym))) ;
            }
            else {
                m.append(" expression non termin√©e") ;
            }

        }
        throw new AnalyseSyntaxiqueException("" + m) ;
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
    }
:};

/* ------------Declaration of Terminals and Non Terminals Section----------- */

/* Terminals (tokens returned by the scanner).  */

terminal Symbol    PROGRAMME, DEBUT, FIN, POINTVIRGULE, ECRIRE, ENTIER, LIRE ;
terminal String    IDF, CSTENTIERE ;

/* Non terminals used in the grammar section.  */

non terminal ArbreAbstrait PROG, LINST, LDEC, CPRG;
non terminal Expression EXP ;
non terminal Instruction DECL_VAR, DECLARATION, INST, DEC;

/* ----------------------------Grammar Section-------------------- */

start with PROG ;

PROG    ::=     PROGRAMME  IDF  DEBUT  CPRG:li  FIN
                {: RESULT = li ; :}
                ;

CPRG   ::=     LDEC:ld LINST:li
               {: ((BlocDInstructions)ld).ajouter(li) ;
                   RESULT = ld ; :}
        |
               LINST:li
               {: RESULT =li; :}
               ;

LDEC   ::=     LDEC:li  DEC:i
                {: ((BlocDInstructions)li).ajouter(i) ;
                   RESULT = li ; :}
        |
                DEC:i
                {: BlocDInstructions b = new BlocDInstructions(ileft + 1) ;
                   b.ajouter(i) ;
                   RESULT = b ; :}
                ;

DEC ::=          DECL_VAR:d POINTVIRGULE
                 {: RESULT = d; :}
                 ;

LINST   ::=     LINST:li  INST:i
                {: ((BlocDInstructions)li).ajouter(i) ;
                   RESULT = li ; :}
        |
                INST:i
                {: BlocDInstructions b = new BlocDInstructions(ileft + 1) ;
                   b.ajouter(i) ;
                   RESULT = b ; :}
                ;

INST    ::=     ECRIRE EXP:e POINTVIRGULE
                {: RESULT = new Ecrire(e, eleft + 1) ; :}
         |
                LIRE IDF:i POINTVIRGULE
                {:  Idf id=new Idf(ileft +1, i) ;
                    RESULT = new Lire(ileft + 1, id) ; :}
                ;


EXP    ::=     CSTENTIERE:c
                {: RESULT = new ConstanteEntiere(c, cleft + 1) ; :}
          |
                IDF:i
                {: RESULT = new Idf(ileft + 1, i) ; :}
                ;

DECL_VAR ::=    ENTIER IDF:i
                {: EntreeVariable e = new EntreeVariable(i, ileft + 1);
                   Tds.getInstance().ajouter(e, new SymboleVariable(Tds.getInstance().getTailleZoneVariable()));
                   RESULT = new Declarer(ileft + 1, i, e);
                :}
                ;